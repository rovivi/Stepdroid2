// app/build.gradle
plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'kotlin-kapt'
}

android {
    compileSdk 35
    namespace 'com.kyagamy.step'

    defaultConfig {
        applicationId "com.kyagamy.step"
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }

    buildFeatures {
        viewBinding true
        compose true
        buildConfig true
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            // Security configurations for release builds
            signingConfig signingConfigs.debug // Replace with proper signing config
        }
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
            pickFirst 'lib/**/*.so'
        }
        resources {
            excludes += [
                    'META-INF/atomicfu.kotlin_module',
                    'META-INF/DEPENDENCIES',
                    'META-INF/LICENSE',
                    'META-INF/LICENSE.txt',
                    'META-INF/NOTICE',
                    'META-INF/NOTICE.txt',
                    'META-INF/*.kotlin_module'
            ]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = "17"
        freeCompilerArgs += [
                "-opt-in=kotlin.RequiresOptIn",
                "-opt-in=androidx.compose.foundation.ExperimentalFoundationApi",
                "-opt-in=androidx.compose.material3.ExperimentalMaterial3Api"
        ]
    }

    // Android 15 specific configurations
    lint {
        checkReleaseBuilds false
        abortOnError false
        disable 'GradleOverrides'
        baseline file("lint-baseline.xml")
    }
}

// Define configuration for natives in the app project scope as well, 
// to match what we used in dependencies block
configurations { natives }

// Task to copy native libraries to jniLibs
tasks.register('copyAndroidNatives') {
    doFirst {
        file("src/main/jniLibs/armeabi-v7a/").mkdirs()
        file("src/main/jniLibs/arm64-v8a/").mkdirs()
        file("src/main/jniLibs/x86/").mkdirs()
        file("src/main/jniLibs/x86_64/").mkdirs()

        configurations.natives.files.each { jar ->
            def outputDir = null
            if (jar.name.endsWith("natives-armeabi-v7a.jar")) outputDir = file("src/main/jniLibs/armeabi-v7a")
            if (jar.name.endsWith("natives-arm64-v8a.jar")) outputDir = file("src/main/jniLibs/arm64-v8a")
            if (jar.name.endsWith("natives-x86.jar")) outputDir = file("src/main/jniLibs/x86")
            if (jar.name.endsWith("natives-x86_64.jar")) outputDir = file("src/main/jniLibs/x86_64")

            if (outputDir != null) {
                copy {
                    from zipTree(jar)
                    into outputDir
                    include "*.so"
                }
            }
        }
    }
}

tasks.whenTaskAdded { packageTask ->
    if (packageTask.name.contains("package")) {
        packageTask.dependsOn 'copyAndroidNatives'
    }
}

repositories {
    google()
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
    // Core desugaring for API compatibility
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.4'

    // Kotlin & Coroutines
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${rootProject.coroutines}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${rootProject.coroutines}"

    // AndroidX essentials
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation "androidx.collection:collection-ktx:1.5.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.2.1"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation "androidx.recyclerview:recyclerview-selection:1.2.0"
    implementation "androidx.multidex:multidex:2.0.1"

    // Material Design
    implementation "com.google.android.material:material:${rootProject.materialVersion}"

    // Navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.9.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.9.0'
    implementation 'androidx.navigation:navigation-compose:2.9.0'

    // Room Database
    implementation "androidx.room:room-runtime:${rootProject.roomVersion}"
    implementation "androidx.room:room-ktx:${rootProject.roomVersion}"
    kapt "androidx.room:room-compiler:${rootProject.roomVersion}"
    androidTestImplementation "androidx.room:room-testing:${rootProject.roomVersion}"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-common-java8:${rootProject.archLifecycleVersion}"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${rootProject.archLifecycleVersion}"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:${rootProject.archLifecycleVersion}"
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.9.1'
    implementation 'androidx.lifecycle:lifecycle-process:2.9.1'

    // Jetpack Compose (BOM)
    def composeBom = platform("androidx.compose:compose-bom:${rootProject.composeBomVersion}")
    implementation composeBom
    androidTestImplementation composeBom
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.foundation:foundation'
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.animation:animation'
    implementation 'androidx.compose.animation:animation-graphics'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
    implementation 'androidx.activity:activity-compose:1.10.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.9.1'
    implementation 'androidx.compose.runtime:runtime-livedata'

    // Security & Permissions
    implementation 'androidx.security:security-crypto:1.1.0-alpha06'
    implementation 'androidx.biometric:biometric:1.2.0-alpha05'
    implementation 'androidx.work:work-runtime-ktx:2.10.1'

    // Third-party libraries
    implementation 'com.github.mveroukis:storage-chooser:2.0.4.4a'
    implementation "io.github.ParkSangGwon:tedpermission-normal:3.3.0"
    implementation 'com.squareup.picasso:picasso:2.8'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    implementation 'io.coil-kt:coil-compose:2.6.0'

    // LibGDX
    implementation "com.badlogicgames.gdx:gdx:1.12.1"
    implementation "com.badlogicgames.gdx:gdx-backend-android:1.12.1"
    natives "com.badlogicgames.gdx:gdx-platform:1.12.1:natives-armeabi-v7a"
    natives "com.badlogicgames.gdx:gdx-platform:1.12.1:natives-arm64-v8a"
    natives "com.badlogicgames.gdx:gdx-platform:1.12.1:natives-x86"
    natives "com.badlogicgames.gdx:gdx-platform:1.12.1:natives-x86_64"

    // Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.14.2'
    //testImplementation 'androidx.arch.core:core-testing:2.2.1'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test:rules:1.6.1'
}